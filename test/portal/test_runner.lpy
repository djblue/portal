(ns portal.test-runner
  (:require [clojure.string :as str]
            [clojure.test :as t]
            [portal.api :as api]
            [portal.client :as p]
            [portal.console :as console]
            [portal.runtime.fs :as fs])
  (:import [sys :as sys]))

(defn- load-test [ns]
  @(future
     (load-file
      (str
       (fs/join (fs/cwd)
                "test"
                (str/join "/" (str/split (str/replace (name ns) #"-" "_") #"\\.")))
       ".cljc"))))

(defn- ->source [v]
  (let [{:keys [file line col]} (meta v)]
    (str file ":" line ":" col)))

(defn- run-tests [& tests]
  (let [summary (atom {:error 0 :fail 0})
        !report (atom [])
        out-fn  (if (p/enabled?) (constantly nil) println)
        report  (fn report [message]
                  (when (p/enabled?)
                    (swap! !report conj
                           (cond-> message
                             :always
                             (assoc :time    (console/now)
                                    :runtime (console/runtime))
                             (:ns message)
                             (update :ns (comp symbol str))))
                    (when (= (:type message) :end-test-ns)
                      (p/submit @!report)
                      (reset! !report []))))]
    (doseq [test-ns tests]
      (report {:type :begin-test-ns :ns test-ns})
      (load-test test-ns)
      (out-fn "\nTesting" (name test-ns))
      (doseq [[_s v] (ns-publics test-ns)
              :when (::t/test (meta v))]
        (report {:type :begin-test-var :var v})
        (try
          (let [failures (:failures (v))]
            (swap! summary update :fail + (count failures))
            (doseq [failure failures]
              (out-fn "\nFAIL in" (str "(" (name v) ")") (->source v))
              (out-fn "expected:" (:expected failure))
              (out-fn "  actual:" (:actual failure))
              (report (merge {:type :fail} (select-keys failure [:message :actual :expected])))))
          (catch Exception e
            (report (merge {:type :error} {:message (ex-message e) :data (ex-data e)}))
            (swap! summary update :error inc)
            e))
        (report {:type :end-test-var :var v}))
      (report {:type :end-test-ns :ns test-ns}))
    (out-fn "\nRan all tests.")
    (out-fn (:fail @summary) "failures" (:error @summary) "errors.")
    @summary))

(defn -main []
  (let [{:keys [fail error]}
        (run-tests
         'portal.runtime.cson-test
         'portal.runtime.fs-test
         'portal.runtime.json-buffer-test
         'portal.runtime.npm-test
         'portal.runtime.shell-test
         'portal.client-test
         'portal.runtime-test
         'portal.runtime.api-test)]
    (api/stop)
    (sys/exit (+ fail error))))

(-main)